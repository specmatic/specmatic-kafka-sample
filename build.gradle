import org.jetbrains.kotlin.gradle.tasks.KotlinCompile

plugins {
    id 'org.springframework.boot' version '3.2.2'
    id 'io.spring.dependency-management' version '1.1.4'
    id 'org.jetbrains.kotlin.jvm' version '1.9.22'
    id 'org.jetbrains.kotlin.plugin.spring' version '1.9.22'
    id "org.jetbrains.kotlin.plugin.serialization" version "2.0.0"
}

group = 'com.example'
version = '0.0.1-SNAPSHOT'

java {
    sourceCompatibility = '17'
}

repositories {
    maven { url uri("lib") }
//    mavenLocal()
    mavenCentral()
}

dependencies {
    implementation ('org.springframework.boot:spring-boot-starter') {
        exclude group: 'ch.qos.logback', module: 'logback-classic'
    }
    implementation 'org.springframework.kafka:spring-kafka:3.2.0'

    implementation('com.fasterxml.jackson.core:jackson-databind:2.16.0')
    implementation('com.fasterxml.jackson.module:jackson-module-kotlin:2.16.0')

    testImplementation('io.specmatic:specmatic-kafka:0.25.0-TRIAL')
    testImplementation("org.assertj:assertj-core:3.24.2")
    testImplementation 'org.junit.jupiter:junit-jupiter:5.6.0'
    testImplementation 'org.testcontainers:testcontainers:1.18.3'
}

configurations.all {
    resolutionStrategy.eachDependency { details ->
        if (details.requested.group == "com.fasterxml.jackson.core") {
            details.useVersion("2.16.0")
        }
    }
}

tasks.withType(KotlinCompile).configureEach {
    kotlinOptions {
        freeCompilerArgs += '-Xjsr305=strict'
        jvmTarget = '17'
    }
}

test {
    useJUnitPlatform()
    environment "PUBSUB_EMULATOR_HOST", "localhost:8085"
    testLogging.showStandardStreams = true
    testLogging {
        showStandardStreams = true
        events "passed", "skipped", "failed"
        afterSuite { desc, result ->
            if (desc.parent == null) {
                println()
                println("-------------------------")
                println("Total tests run: ${result.testCount}")
                println("Tests passed: ${result.successfulTestCount}")
                println("Tests failed: ${result.failedTestCount}")
                println("Tests skipped: ${result.skippedTestCount}")
                println("-------------------------")
                println()
            }
        }
    }
}
